// Motoko Un-Grammar.
//
// This grammar specifies the structure of Motoko's concrete syntax tree.
// It does not specify parsing rules (ambiguities, precedence, etc are out of scope).
// Tokens are processed -- contextual keywords are recognised, compound operators glued.
//
// Legend:
//
//   //          -- comment
//   Name =      -- non-terminal definition
//   'ident'     -- token (terminal)
//   A B         -- sequence
//   A | B       -- alternation
//   A*          -- zero or more repetition
//   A?          -- zero or one repetition
//   (A)         -- same as A
//   label:A     -- suggested name for field of AST node

// Names

Name =
  'ident'

// Literals
Literal =
  'number_lit' | 'true' | 'false' | 'null'

// Types

Type =
    OptionalType
  | ParenType
  | AsyncType
  | PathType
  | TupleType
  | ArrayType
  | FuncType
  | ObjectType
  | VariantType
  | NamedType
  | PrimType

OptionalType =
  '?' Type

ParenType =
  '(' Type ')'

AsyncType =
  'async' Type

PathType =
  Path TypeArgs?

TypeArgs =
  '<' args:(Type (',' Type)*) '>'

Path = Name

TupleType =
  '(' fields:(Type (',' Type)*)? ')'

ArrayType =
  '[' Type ']'

FuncType =
  'shared'? 'query'? TypeParams? FuncArg '->' FuncResult

FuncArg = Type
FuncResult = Type

TypeParams =
  '<' params:(TypeBind (',' TypeBind)*) '>'

TypeBind =
  Path TypeBound?

TypeBound =
  '<:' Type

ObjectType =
  ObjectSort? '{' fields:(TypeField (',' TypeField)*)? '}'

ObjectSort =
    'object'
  | 'class'
  | 'actor'

ObjectField =
    TypeField
  | TypeFieldFunc

TypeField = 'var'? Name ':' Type
// Does this have to be a functype?
TypeFieldFunc = Name TypeParams? FuncType

VariantType =
  '{' variants:(TypeTag (',' TypeTag)*)? '}'

TypeTag =
  '#' Name (':' Type)?

NamedType =
  Name ':' Type

PrimType =
  'prim' Name

// Patterns

Pattern =
    WildcardPat
  | VarPat
  | LiteralPat
  | ParenPat
  | TuplePat

WildcardPat = '_'
VarPat = Name
LiteralPat = Literal
ParenPat = '(' Pattern ')'
TuplePat = '(' patterns:(Pattern (',' Pattern)*)? ')'
